apply plugin: 'jacoco'

jacoco { toolVersion "0.8.4" }

android {
    testBuildType "automationTest"

    buildTypes {
        debug {
        }

        automationTest {
            initWith(buildTypes.debug)
            testCoverageEnabled = true
        }

        release {
            initWith(buildTypes.debug)
        }
    }

    sourceSets {
        debug.java.srcDirs += 'src/debug/java'
        automationTest.java.srcDirs += 'src/debug/java'
        release.java.srcDirs += 'src/debug/java'
    }

    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet true// if true, stop the gradle build if errors are found
        abortOnError false// do not ignore warnings
        warningsAsErrors true
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

//Unit Test Coverage filtered
task createUnitTestReport(type: JacocoReport, dependsOn: ['testAutomationTestUnitTest']) {
    reports {
        html.enabled = true
    }
    def fileFilter = [
            //Android stuff
            '**/R.class',
            '**/BR.class',
            '**/R$*.class',
            '**/BR$*.class',
            '**/BuildConfig.*',
            'android/**/*.*',
            '**/Manifest*.*',
            //Data Binding
            '**/*databinding/**/*.*',
            //Test mocks
            '**/*Mock.*',
            '**/*Test*.*',
            //Data classes (without any logic)
            "**/services/**/model/**"]

    //To support Java coverage on Unit tests
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/automationTest", excludes: fileFilter)
    //To support Kotlin coverage on Unit tests
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/automationTest", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"
    def debugSrc = "${project.projectDir}/src/debug/java"
    sourceDirectories = files([mainSrc, debugSrc])
    classDirectories = files([debugTree], [kotlinDebugTree])
    executionData = files("${buildDir}/jacoco/testAutomationTestUnitTest.exec")
}